title: Sally
tags: 
colorID: 0
position: 524,111
---
<<declare $times_talked_sally_before_ship = 0>>
<<declare $player_avoided_ship = $times_talked_sally_before_ship > 3>>
<<once>>
    Player: Hey, Sally. #line:794945
    // these HTML-style tags will only work with the ConvertHTMLToBBCode setting 
    // enabled on the provided example AsyncLineView
    Sally: <wave freq=20 amp=14>Uah!</wave> #line:2dc39b
    Sally: <shake rate=20.0 level=5 connected=1>You snuck up on me!</shake> #line:34de2f
    Sally: Don't do that. #line:dcc2bc
<<endonce>>
<<jump Sally_Give_Quest>>
===

title: Sally_Give_Quest
---
<<set $times_talked_sally_before_ship = $times_talked_sally_before_ship +1 >>
// smart variable example 
<<if $player_avoided_ship and not $sally_warning>>
<<once>>
Sally: Hey! Stop talking to me and talk to the ship already!  #line:02aa0c8 
Sally: Sigh...  #line:0255785 
<<endonce>>
<<endif>>
    => Sally: Hi.   #line:0af7f86 
    => Sally: What's up.  #line:077a332 
    => Sally: Yo.  #line:0b00ed4 
-> Anything exciting happen on your watch? <<if not visited("Sally_Watch")>> #line:5d7a7c
    <<jump Sally_Watch>>
-> Sorry about the console. <<if $sally_warning and not visited("Sally_Sorry")>> #line:0a7e39
    <<jump Sally_Sorry>>
-> See you later. #line:0facf7
    <<jump Sally_Exit>>
===

title: Sally_Watch
tags: 
colorID: 0
position: 512,430
---
Sally: Not really.  #line:8c3f98
Sally: Same old nebula, doing the same old thing. #line:24c418
Sally: Oh, Ship wanted to see you. Go say hi to it. #line:df4eaf
Sally: Bye. #shadow:60c282     
<<set $should_see_ship to true>>
<<if visited("Ship") is true>>
    Player: Already done! #line:1fea6c
    Sally: Go say hi again. #line:5df323
<<endif>>
===

title: Sally_Exit
tags: 
colorID: 6
position: 211,417
---
Sally: Bye. #line:60c282
===

title: Sally_Sorry
tags: 
colorID: 0
position: 827,439
---
<<declare $apologized_to_sally to false as bool>>
Sally: Yeah. Don't do it again. #line:d7df49
<<set $apologized_to_sally to true>>
===

